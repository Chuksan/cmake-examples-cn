= Static Library
:toc:
:toc-placement!:

toc::[]

# Introduction

Shows a hello world example which first creates and links a static library. This is a 
simplified example showing the library and binary in the same folder. Typically
these would be in sub-projects as described in section link:../../02-sub-projects[02-sub-projects]

展示了一个hello world示例，该示例首先创建并链接一个静态库。这是一个显示了在同一文件夹下的库和二进制文件的简单示例。通常这些应该归于 sub-projects 主题下，它们将在 link:../../02-sub-projects[02-sub-projects] 具体描述。

The files in this tutorial are below:

```
$ tree
.
├── CMakeLists.txt
├── include
│   └── static
│       └── Hello.h
└── src
    ├── Hello.cpp
    └── main.cpp
```

  * link:CMakeLists.txt[] - Contains the CMake commands you wish to run
  * link:include/static/Hello.h[] - The header file to include
  * link:src/Hello.cpp[] - A source file to compile
  * link:src/main.cpp[] - The source file with main


# Concepts

## Adding a Static Library

The +add_library()+ function is used to create a library from some source files.
This is called as follows:

+add_library()+ 函数用于从某些源文件创建一个库。
可以如下调用：

[source,cmake]
----
add_library(hello_library STATIC 
    src/Hello.cpp
)
----

This will be used to create a static library with the name libhello_library.a with
the sources in the +add_library+ call.

这将使用 +add_library+ 调用中的源代码，创建名称为 libhello_library.a 的静态库。

[NOTE]
====
As mentioned in the previous example, we pass the source files directly to the
+add_library+ call, as recommended for modern CMake.

如前一个示例所述，我们将源文件名直接传递给 +add_library+调用，遵从现代CMake的建议。
====

## Populating Including Directories

In this example, we include directories in the library using the +target_include_directories()+ function with the scope set to +PUBLIC+.

在此示例中，我们使用 +target_include_directories()+ 函数将目录包含在库中，且范围设置为 +PUBLIC+ 。

[source,cmake]
----
target_include_directories(hello_library
    PUBLIC 
        ${PROJECT_SOURCE_DIR}/include
)
----

This will cause the included directory used in the following places:

* When compiling the library
* When compiling any additional target that links the library.

The meaning of scopes are:

* +PRIVATE+ - the directory is added to this target's include directories
* +INTERFACE+ - the directory is added to the include directories for any targets that link this library.
* +PUBLIC+ - As above, it is included in this library and also any targets that link this library.

这将导致在以下情况使用包含的目录：

* 编译这个库时
* 编译链接这个库的任何其他目标时。

scopes 的含义:

* +PRIVATE+ - 该目录已添加到该目标的包含目录
* +INTERFACE+ - 该目录将添加到链接此库的所有目标的包含目录中。
* +PUBLIC+ - 如上所述，这个目录已包含在此库中，也包含到了链接到该库的所有目标中。

[TIP]
====
For public headers it is often a good idea to have your include folder be "namespaced"
with sub-directories. 

对于 public 的头文件，通常最好将 include 的文件夹与子目录放在一个命名空间中。 

The directory passed to +target_include_directories+ will be the root of your 
include directory tree and your C++ files should include the path from there to your header.

传递给 +target_include_directories+ 的目录将是您include 目录树的的根目录，您的 C++ 文件也要按照从那里到头文件的路径来包含头文件。

For this example you can see that we do it as follows:
[source,cpp]
----
#include "static/Hello.h"
----

Using this method means that there is less chance of header filename clashes when
you use multiple libraries in your project. 

使用此方法意味着当您在项目中使用多个库时，能够降低头文件名冲突的可能性。

====

## Linking a Library

When creating an executable that will use your library you must tell the compiler
about the library. This can be done using the +target_link_library()+ function.

创建将使用您的库的可执行文件时，必须告诉编译器(要链接哪个库)。可以使用 +target_link_library()+ 函数来完成。

[source,cmake]
----
add_executable(hello_binary 
    src/main.cpp
)

target_link_libraries( hello_binary
    PRIVATE  
        hello_library
)
----

This tells CMake to link the hello_library against the hello_binary executable
during link time. It will also propagate any include directories with +PUBLIC+ or +INTERFACE+ scope
 from the linked library target.

这里告诉CMake，在链接时，将hello_library链接到hello_binary可执行文件。它还将传播来自被链接的库目标在 +PUBLIC+ 或 +INTERFACE+ 范围的任何包含目录。

An example of this being called by the compiler is

```
/usr/bin/c++ CMakeFiles/hello_binary.dir/src/main.cpp.o -o hello_binary -rdynamic libhello_library.a
```


# Building the Example

[source,bash]
----
$ mkdir build

$ cd build

$ cmake ..
-- The C compiler identification is GNU 4.8.4
-- The CXX compiler identification is GNU 4.8.4
-- Check for working C compiler: /usr/bin/cc
-- Check for working C compiler: /usr/bin/cc -- works
-- Detecting C compiler ABI info
-- Detecting C compiler ABI info - done
-- Check for working CXX compiler: /usr/bin/c++
-- Check for working CXX compiler: /usr/bin/c++ -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Configuring done
-- Generating done
-- Build files have been written to: /home/matrim/workspace/cmake-examples/01-basic/C-static-library/build

$ make
Scanning dependencies of target hello_library
[ 50%] Building CXX object CMakeFiles/hello_library.dir/src/Hello.cpp.o
Linking CXX static library libhello_library.a
[ 50%] Built target hello_library
Scanning dependencies of target hello_binary
[100%] Building CXX object CMakeFiles/hello_binary.dir/src/main.cpp.o
Linking CXX executable hello_binary
[100%] Built target hello_binary

$ ls
CMakeCache.txt  CMakeFiles  cmake_install.cmake  hello_binary  libhello_library.a  Makefile

$ ./hello_binary
Hello Static Library!
----
